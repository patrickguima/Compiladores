PARSER_BEGIN(Lugosi)
import java.io.*;
import java.util.*;

class ArvoreLugosi{//
	Main main;
	ArrayList <Func> funcoes;

    ArvoreLugosi(Main m, ArrayList<Func> f){
        this.main = m; 
        this.funcoes = f;
    }
}

class Main{//
	ArrayList <VarDecl> varD;
	ArrayList <Comando> comandos;

    Main(ArrayList <VarDecl> varD, ArrayList <Comando> comandos){
        this.varD = varD;
        this.comandos = comandos;
    }
}

class VarDecl{ //possivel erro - Tipo tipo ou String tipo
	String tipo;
	String var;

    VarDecl(Token id, String tipo){
        this.var = id.image;
        this.tipo = tipo;
    }
}

class SeqComandos{//
    ArrayList <Comando> comandos;

    SeqComandos(ArrayList <Comando> c) {
        this.comandos = c;
    }
}

class Comando{}//
class CmdAtribuicao extends Comando{ // x = 4 + a;
	String var;  
	Exp e;

    CmdAtribuicao(Token id, Exp cmd){ 
        this.var = id.image;
        this.e = cmd;
    }
}

class CmdChamadaFuncao extends Comando{ // titi(int a, char b);
	String nomeFuncao;
	ArrayList <Exp> argumentos;

    CmdChamadaFuncao(Token id, ArrayList <Exp> arg){
        this.nomeFuncao = id.image;
        this.argumentos = arg;
    }
}

class CmdIf extends Comando{//
	Exp exp;
	ArrayList <Comando> then;

    CmdIf(Exp e, ArrayList <Comando> seqComandos){
        this.exp = e;
        this.then = seqComandos;
    }
}
class CmdWhile extends Comando{//
	Exp exp;
	ArrayList <Comando> comandos;

    CmdWhile(Exp e, ArrayList <Comando> seqComandos){
        this.exp = e;
        this.comandos = seqComandos;
    }
}
class CmdDoWhile extends Comando{//
	ArrayList <Comando> comandos;
	Exp exp;

    CmdDoWhile(Exp e, ArrayList <Comando> seqComandos){
        this.comandos = seqComandos;
        this.exp = e;
    }
}
class CmdReturn extends Comando{//
	Exp exp;

    CmdReturn(Exp e){
        this.exp = e;
    }
} 
class CmdPrint extends Comando{//
	Exp exp;
    CmdPrint(Exp e){
        this.exp = e;
    }
}

class Exp{}//
class ExpOperador extends Exp{//
	Exp arg1;
	Exp arg2;
	String operador;

    ExpOperador(Exp a1, Exp a2, String operador){
        this.arg1 = a1;
        this.arg2 = a2;
        this.operador = operador;
    }
}
class Fator extends Exp{}//
class FtrTokenId extends Fator{//
	String id;

    FtrTokenId(String id){
        this.id = id;
    }
}
class FtrExpPar extends Fator{//
	String id;
	ArrayList <Exp> listExp;

    FtrExpPar(String id, ArrayList <Exp> listExp){
        this.id = id;
        this.listExp = listExp;
    }
}

class FtrNumLiteral extends Fator {//
    String numLiteral;

    FtrNumLiteral(String numLiteral){
        this.numLiteral = numLiteral;
    }
}

class FtrTipo extends Fator{//
    String tipo;

    FtrTipo(String tipo){
        this.tipo = tipo;
    }
}

class ListaExp{//
    Exp e;

    ListaExp(Exp e){
        this.e = e;
    }
}

class Func{ //
    String id;
    String tipo;
    ArrayList <ListaArg> argumentos;
    ArrayList <VarDecl> vd;
    ArrayList <Comando> seqComandos;

    Func(Token id, String tipo, ArrayList <ListaArg> argumentos, ArrayList <VarDecl> vd, ArrayList <Comando> seqComandos){
        this.id = id.image;
        this.tipo = tipo;
        this.argumentos = argumentos;
        this.vd = vd;        
        this.seqComandos = seqComandos;
    }
}

class ListaArg{
    String tipo;
    String id;

    ListaArg(String tipo, Token id){
        this.tipo = tipo;
        this.id = id.image;
    }
}

public class Lugosi {
    public static void main(String args[]) throws Exception{//revisado
        
        Lugosi parser = new Lugosi(new FileInputStream(args[0]));
        ArvoreLugosi lug = parser.Lugosi();
        printerInicio(lug);
    }

    public static void printerInicio(ArvoreLugosi lug){//revisado
        
        System.out.print("#include <iostream>\n");
        System.out.println("\nusing namespace std;");

        for(Func f : lug.funcoes){ 
            printerFunc(f);
        }

        printerMain(lug.main);
    }

    public static void printerMain(Main main){//revisado
		System.out.print("int main()"); 
		System.out.println("{ ");

		for(VarDecl vardecl: main.varD){
			printerVarDecl(vardecl);
		}
		printerSeqComandos(main.comandos);
		
		System.out.println("return 0; \n}");
	}

    public static void printerFunc(Func f){//revisado
        int cont = 0;
        System.out.print(f.tipo + " ");
        System.out.print(f.id + " (");

        for(ListaArg arg: f.argumentos){
            printerArgumentos(arg);
            if((f.argumentos).size() > 1 && cont < (f.argumentos).size() - 1){
                System.out.print(", ");
            }
            cont+=1;
        }

        System.out.println("){");

        for(VarDecl v : f.vd){ 
            printerVarDecl(v);
        }

        for(Comando c : f.seqComandos){
            printerCmd(c);
        }
        System.out.println("}");
    }

    public static void printerArgumentos(ListaArg arg){//revisado
        	System.out.print(arg.tipo + " ");
		    System.out.print(arg.id);
    }

    public static void printerVarDecl(VarDecl v){
        System.out.println(v.tipo +" "+ v.var +"; ");
    }

    public static void printerCmd(Comando c){//revisado
        
        if (c instanceof CmdAtribuicao){
			printerCmdAtribuicao(c);
		}
        else if (c instanceof CmdChamadaFuncao){
			printerCmdChamadaFuncao(c);
		}
		else if (c instanceof CmdIf){
			printerCmdIf(c);
		}
		else if (c instanceof CmdWhile){
			printerCmdWhile(c);
		}
		else if (c instanceof CmdDoWhile){
			printerCmdDoWhile(c);
		}
		else if (c instanceof CmdReturn){
			printerCmdReturn(c);
		}
		else if (c instanceof CmdPrint){
			printerCmdPrint(c);
		}
    }

    public static void printerCmdNR(Object e){//revisado
        if(e instanceof Exp){
            System.out.print("= ");
            printerExp((Exp) e);
            System.out.println(";");
        }
		else if (e instanceof ArrayList){
			System.out.print(" (");
			int tamanhoExp = ((ArrayList <Exp>)e).size(); 
			int cont = 0;
		
			for (Exp expressao : ((ArrayList <Exp>)e)) {
				printerExp(expressao);
				if (tamanhoExp > 1 && cont < tamanhoExp -1){
					System.out.print(", ");
				}
				cont+= 1;
			}
			System.out.print(")");
			System.out.println("; ");		
		}
    }
    
    public static void printerCmdAtribuicao(Comando c){//revisado
		System.out.print(((CmdAtribuicao) c).var + " ");
		printerCmdNR(((CmdAtribuicao) c).e);
    }

    public static void printerCmdChamadaFuncao(Comando c){//revisado
		System.out.print(((CmdChamadaFuncao) c).nomeFuncao + " ");
		printerCmdNR(((CmdChamadaFuncao) c).argumentos);
	}

    public static void printerCmdIf(Comando c){//revisado
        System.out.print("if (");
		printerExp(((CmdIf) c).exp);
		System.out.print(") ");
		System.out.println("{ ");
		printerSeqComandos(((CmdIf) c).then); 
		System.out.print("}");
		System.out.println(";");
    }

    public static void printerCmdWhile(Comando c){//revisado
        System.out.print("while ");
		System.out.print("(");
		printerExp((((CmdWhile) c).exp));
		System.out.print(") ");
		System.out.print("do ");
		System.out.println("{ ");
		printerSeqComandos(((CmdWhile) c).comandos); 
		System.out.print("}");
		System.out.println(";");
    }

    public static void printerCmdDoWhile(Comando c){//revisado
        System.out.print("do ");
		System.out.println("{ ");
		printerSeqComandos(((CmdDoWhile) c).comandos); 
		System.out.print("}");
		System.out.print("while ");
		System.out.print("(");
		printerExp(((CmdDoWhile) c).exp);
		System.out.print(")");
		System.out.println(";");
    }

    public static void printerCmdReturn(Comando c){//revisado
        System.out.print("return ");	
		printerExp(((CmdReturn) c).exp);
		System.out.println(";");
    }

    public static void printerCmdPrint(Comando c){//revisar 
        System.out.print("cout << ");
		printerExp(((CmdPrint) c).exp);
		System.out.print("<<");
		System.out.println(";");
    }

    public static void printerSeqComandos(ArrayList <Comando> comandos){//revisado
		for (Comando c : comandos) {
			printerCmd(c);
		}
	}


    public static void printerExp(Exp e){//revisado
        if(e instanceof Fator){
            printerFtr(e);
        }
        else{
            printerExpOperador(e);
        }
    }

    public static void printerFtr(Exp e){//revisado
        if (e instanceof FtrExpPar){
			printerFtrExpPar(e);
		}
		else if (e instanceof FtrNumLiteral){
			printerFtrNumLiteral(e);
		}
		else if (e instanceof FtrTipo){
			printerFtrTipo(e);
		}
		else if (e instanceof FtrTokenId){
			printerFtrTokenId(e);
		}
    }

    public static void printerFtrExpPar(Exp e){//revisado
        System.out.print( ((FtrExpPar) e).id + " ");
		
		int tamanhoExp = (((FtrExpPar)e).listExp).size(); 
		int cont = 0;
		
		for (Exp e1 : ((FtrExpPar)e).listExp){
			printerFtr(e1);
			if (tamanhoExp > 1 && cont < tamanhoExp-1){
				System.out.print(", ");
			}
			cont+= 1;
		}
    }

    public static void printerFtrTokenId(Exp e){//revisado
        System.out.print( ((FtrTokenId) e).id + " ");
    }

    public static void printerFtrNumLiteral(Exp e){//revisado
		System.out.print( ((FtrNumLiteral) e).numLiteral + " ");
	}

    public static void printerExpOperador(Exp e){ //revisado
        System.out.print("(");
	  	printerExp(((ExpOperador)e).arg1);
	  	System.out.print(((ExpOperador) e).operador + " ");
	  	printerExp(((ExpOperador)e).arg2);	
	  	System.out.print(")");
    }

    public static void printerFtrTipo(Exp e){//revisado
		System.out.print(((FtrTipo) e).tipo + " ");
	}

}

PARSER_END(Lugosi)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN :
{
  <MAIN: "main">
| <ACHAVES: "{">
| <FCHAVES: "}">
|<VAR: "var">
|<PONTOVIR: ";">
|<TIPO: ("int" | "bool")>
|<IF: "if">
|<WHILE: "while">
|<DO: "do">
|<RETURN: "return">
|<PRINT: "print">
|<ATRIBUICAO:":=">
|<APARENTESES:"(">
|<FPARENTESES:")">
|<FUNCTION:"function">
|<VIRGULA:",">
|<TOKEN_NUMLITERAL:(["0"-"9"])+ ("."(["0"-"9"])+ )? ("E" ("+" | "-")? (["0"-"9"])+)?>
|<TRUE: "true">
|<FALSE: "false">
|<TOKEN_ID: ["a"-"z","A"-"Z"] ( ["a"-"z","A"-"Z","0"-"9"])* ("_")*>

}

TOKEN :
{
  <NUM : (["0"-"9"])+>
 |<ID: ["a"-"z","A"-"Z"] ( ["a"-"z","A"-"Z","0"-"9"])*>
 |<OP: "+" | "-" | "*" | "/" | "&&" | "||" | "<" | ">" | "==">
 

}
///////////////////////////////////////////////////////////////////////////////////

ArvoreLugosi Lugosi () : //ok
{Main main; ArrayList <Func> funcoes = new ArrayList<Func>();}
{ 

  main = Main() (funcoes = Func())? <EOF> 
  {return new ArvoreLugosi(main, funcoes);}
}

ArrayList <VarDecl> VarDecl(): //ok
{ ArrayList <VarDecl> a = new ArrayList<VarDecl> (); String tipo; String var; Token id;}
{
	(<VAR> tipo = Tipo() id = <TOKEN_ID> <PONTOVIR> {a.add(new VarDecl(id, tipo));})*
    {return a;}
}

String Tipo(): //ok
{Token t;}
{
  t = <TIPO>
  {return t.image;}
}

ArrayList <Comando> SeqComandos(): //ok
{ ArrayList <Comando> c = new ArrayList <Comando> (); Comando comando;}
{
	(comando = Comando() {c.add(comando);})* 
    {return c;}
}
Comando Comando()://ok
{Token id; Exp e; Object comandoNR; ArrayList <Comando> seqComandos; ArrayList <Exp> listExp;}
{
	((id = <TOKEN_ID>) comandoNR = ComandoNR())
        {if (comandoNR instanceof Exp) return new CmdAtribuicao(id, ((Exp) comandoNR));
         else return new CmdChamadaFuncao(id, ((ArrayList<Exp>  ) comandoNR));  }
  	|(<IF> <APARENTESES> (e = Exp()) <FPARENTESES> <ACHAVES> (seqComandos = SeqComandos()) <FCHAVES> <PONTOVIR>)
        {return new CmdIf(e, seqComandos);}
  	|(<WHILE> <APARENTESES> (e = Exp()) <FPARENTESES> <DO> <ACHAVES> (seqComandos =  SeqComandos()) <FCHAVES> <PONTOVIR>)
        {return new CmdWhile(e, seqComandos);}
  	|(<DO> <ACHAVES> (seqComandos = SeqComandos()) <FCHAVES> <WHILE> <APARENTESES> (e = Exp()) <FPARENTESES> <PONTOVIR>)
        {return new CmdDoWhile(e, seqComandos);}
  	|(<RETURN> (e = Exp()) <PONTOVIR>)
        {return new CmdReturn(e);}
  	|(<PRINT> <APARENTESES> (e = Exp()) <FPARENTESES> <PONTOVIR>)
        {return new CmdPrint(e);}
}
ArrayList<Func> Func(): //ok
{ ArrayList<Func> func = new ArrayList<Func> (); Token id; String tipo; ArrayList <ListaArg> argumentos;ArrayList <VarDecl> varD;ArrayList <Comando> seqComandos;}
{
    (<FUNCTION> tipo = Tipo() id = <TOKEN_ID> <APARENTESES> ( argumentos = ListaArg()) <FPARENTESES>
        <ACHAVES> (varD = VarDecl()) (seqComandos =  SeqComandos()) <FCHAVES> {func.add(new Func( id, tipo, argumentos, varD, seqComandos));})+
                                                                            
    {return func;}
}

ArrayList<ListaArg> ListaArg(): //ok
{ArrayList<ListaArg> lista = new ArrayList<ListaArg>(); String tipo1; String tipo2; Token id1; Token id2;}
{
    tipo1 = Tipo() id1 = <TOKEN_ID> (<VIRGULA> tipo2 = Tipo() id2 = <TOKEN_ID> {lista.add(new ListaArg(tipo2, id2));})*
    {lista.add(0, new ListaArg(tipo1, id1)); return lista;}
}


// pra tirar a recurs√£o
Object ComandoNR(): //ok
{Object e;}
{
	(<ATRIBUICAO> (e = Exp()) <PONTOVIR>) 
    {return e;}

  	|(<APARENTESES> (e = ListaExp()) <FPARENTESES> <PONTOVIR>)
    {return e;}
}

Exp Exp(): //ok
{Exp arg1; Exp arg2; Token operador;Fator f;}
{
	(<APARENTESES> arg1 = Exp() operador = <OP> arg2 = Exp() <FPARENTESES>)
    {return new ExpOperador(arg1, arg2, operador.image);}
    
    | f = Fator()
    {return f;}
}

ArrayList<Exp> ListaExp()://ok
{Exp e; Exp ex; ArrayList <Exp> lista2 = new ArrayList <Exp>();}
{
  e = Exp() (<VIRGULA> ex = Exp() {lista2.add(ex);})*
  {lista2.add(0, e); return lista2;}
}

/*void listaExpNR()://talvez nao precise
{}
{
  (<VIRGULA> exp() listaExpNR())?
}*/


Fator Fator()://ok
{Token id; Token num; Token tipo; ArrayList<Exp> fNR;}
{
	(id = <TOKEN_ID> fNR = FatNR())
        {if(fNR.isEmpty()) return new FtrTokenId(id.image);
            else return new FtrExpPar(id.image, fNR);}

	|num = <TOKEN_NUMLITERAL> 
        {return new FtrNumLiteral(num.image);}
    
    | tipo = <TRUE> 
        {return new FtrTipo(tipo.image);}
    | tipo = <FALSE> 
        {return new FtrTipo(tipo.image);}
}

ArrayList<Exp> FatNR()://ok
{ArrayList<Exp> lista = new ArrayList<Exp>();}
{
	(<APARENTESES> lista = ListaExp() <FPARENTESES>)?

    {return lista;}
}


Main Main (): //ok
{ ArrayList <VarDecl> varD; ArrayList <Comando> comandos; }
{
 <MAIN> <ACHAVES> 
 
  varD = VarDecl()
  comandos = SeqComandos()

 <FCHAVES>
 { return new Main(varD, comandos); }

}